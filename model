set terminal postscript
set dummy units, per_capita_wages, employment, year

f(units, per_capita_wages, employment, year) = m * units ** units_e * per_capita_wages ** pci_e * employment ** employment_e * year ** year_e

set logscale y

# 1: year
# 2: median rent
# 3: housing units extant
# 4: net new housing units
# 5: employment in city
# 6: total wages of these employees
# 7: consumer price index

FIT_LIMIT=1e-6
fit log(f(units, per_capita_wages, employment, year)) "combined" using 3:($6 / $7 / $5):5:1:(log($2 / $7)) via m, units_e, pci_e, employment_e, year_e
set xlabel "Year"

set ylabel "CPI-adjusted median advertised asking rent"
set xrange [1975:2016]
plot "combined" using 1:($2 / $7) title "Actual", "combined" using 1:(f($3, $6 / $7 / $5, $5, $1)) with lines title "Predicted"

set ylabel "Unexplained variation in rent"
plot "combined" using 1:($2 / $7 / f($3, $6 / $7 / $5, $5, $1)) with lines
