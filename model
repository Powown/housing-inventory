set terminal postscript

set logscale y

# 1: year
# 2: median rent
# 3: housing units extant
# 4: net new housing units
# 5: employment in city
# 6: total wages of these employees
# 7: consumer price index

set xlabel "Year"
set ylabel "Median advertised asking rent"
set xrange [1946:2018]
plot "combined" using 1:2 title "Advertised rent", 78 * 1.066 ** (x - 1956)

set ylabel "CPI-adjusted (1983-1984 dollars) median advertised rent"
plot "combined" using 1:($2 / $7) title "CPI-adjusted advertised rent", 316 * 1.025 ** (x - 1956)



unset logscale y
set xrange [1906:2015]
set yrange [0:]
set ylabel "Housing units"
plot "combined" using 1:4 with steps title "Net new housing units constructed"

set ylabel "Housing units"
plot "combined" using 1:3 with steps title "Total housing units existing"

unset yrange



set dummy units, per_capita_wages, employment, year

f(units, per_capita_wages, employment, year) = m * units ** units_e * per_capita_wages ** pci_e * employment ** employment_e * year ** year_e

unset xrange
FIT_LIMIT=1e-6
fit log(f(units, per_capita_wages, employment, year)) "combined" using 3:($6 / $7 / $5):5:1:(log($2 / $7)) via m, units_e, pci_e, employment_e, year_e

set logscale y
set ylabel "CPI-adjusted median advertised asking rent"
set xrange [1975:2016]
plot "combined" using 1:($2 / $7) title "Actual", "combined" using 1:(f($3, $6 / $7 / $5, $5, $1)) with lines title "Predicted"

set ylabel "Unexplained variation in rent"
set yrange [2.0/3:1.5]
set ytics (2.0/3, 1/1.25, 1, 1.25, 1.5)
plot "combined" using 1:($2 / $7 / f($3, $6 / $7 / $5, $5, $1)) with lines
